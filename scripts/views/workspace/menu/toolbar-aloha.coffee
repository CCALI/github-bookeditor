define [
  'jquery'
  'marionette'
  'aloha'
  'hbs!templates/workspace/menu/toolbar-aloha'
  'hbs!templates/workspace/menu/toolbar-popover'
], ($, Marionette, Aloha, toolbarTemplate, toolbarPopover) ->

  return new class ToolbarAlohaView extends Marionette.ItemView
    template: toolbarTemplate
    tagName: 'span'

    onRender: ->
      # If someone clicks the heading button, don't reload the page
      @$el.find('.btn.currentHeading').on 'click', (e) -> e.preventDefault()

      # If any action is marked with data-content, add a popover to it to
      # aid discoverability. If a flash-action event is received for this
      # action, generated by the toolbar when a plugin requests that the user's
      # attention be drawn to a button, show the popover. But only do it once.
      @$el.find('.action[data-content]')
      .popover(
        placement: 'bottom',
        trigger: 'manual'
        html: true
        template: toolbarPopover({})
        container: '#menu-and-content')
      .on 'shown', (e1) ->
        $(e1.target).data('popover').$tip.find('.close').off('click').on 'click', (e2) ->
          e2.preventDefault()
          $(e1.target).popover('hide')

      @$el.one 'flash-action', (e) -> $(e.target).popover('show')

      # Wait until Aloha is started before enabling the toolbar
      @$el.addClass('disabled')
      Aloha.ready => @$el.removeClass('disabled')
